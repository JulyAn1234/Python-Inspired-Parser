PROGRAM --> IMPORT_STATEMENT PROGRAM_BODY
IMPORT_STATEMENT --> (LibImport ID LineEnder IMPORT_STATEMENT) | ε
PROGRAM_BODY --> (PROGRAM_BODY_ALPHA PROGRAM_BODY) | ε
PROGRAM_BODY_ALPHA --> FUNCTION_STATEMENT | CLASS_STATEMENT

CLASS_STATEMENT --> ClassDef ID Init CLASS_BODY Ender
CLASS_BODY --> CLASS_PROPERTIES CLASS_METHODS
CLASS_PROPERTIES --> (ClassProp Init PROPERTY_LINES Ender) | ε
PROPERTY_LINES --> (ACCESS_MOD VAR_INIT LineEnder PROPERTY_LINES) | ε
ACCESS_MOD --> AccessMod | ε
CLASS_METHODS --> (ClassMethod Init METHODS_BODY Ender) | ε
METHODS_BODY --> (ACCESS_MOD FUNCTION_STATEMENT METHODS_BODY)| ε

FUNCTION_STATEMENT --> FuncDef DataType ID Init FUNCTION_BODY RETURN_LINE Ender
FUNCTION_BODY --> PARAMETER_STATEMENT FUNCTION_BEHAVIOR
PARAMETER_STATEMENT --> (FuncParameter Init PARAMETER_BODY Ender) | ε
PARAMETER_BODY --> (VAR_INIT LineEnder PARAMETER_BODY) | ε
RETURN_LINE --> (Return EXPRESSION LineEnder) | ε

FUNCTION_BEHAVIOR --> (FUNCTION_BEHAVIOR_ALPHA FUNCTION_BEHAVIOR) | ε
FUNCTION_BEHAVIOR_ALPHA --> FUNCTION_LINE | IF_STATEMENT | LOOP

LOOP --> DO While BOOL_EXPRESSION Init FUNCTION_BEHAVIOR WHENEVER Ender
DO --> Do | ε
WHENEVER --> (WhenEver BOOL_EXPRESSION Init FUNCTION_BEHAVIOR WHENEVER) | ε

IF_STATEMENT --> Condicional BOOL_EXPRESSION Init FUNCTION_BEHAVIOR ELSES Ender
ELSES --> (ElseCond BOOL_EXPRESSION Init FUNCTION_BEHAVIOR ELSES) | (Else Init FUNCTION_BEHAVIOR) | ε

FUNCTION_LINE --> FUNCTION_LINE_ALPHA LineEnder
FUNCTION_LINE_ALPHA --> VAR_ASSIGN | VAR_INIT | FUNCTION_CALL | INLINE_FUNCTION | OBJECT_INIT | ε
OBJECT_INIT --> ID ID Asignar New FUNCTION_CALL
INLINE_FUNCTION --> FX | SELF | RECURSIVE
FX --> FxWord ID InGroup MULTI_ID EndGroup Asignar NUMERICAL_EXPRESSION
RECURSIVE --> RecursiveWord ID Asignar DataType OPERATORS
OPERATORS --> STAR | PLUS | MINUS | SLASH | RelOp | LogOp
SELF --> SelfWord ID InGroup OPERATORS Comma Entero EndGroup
VAR_INIT --> DataType MULTI_ID
MULTI_ID --> ID COMMA_ID
COMMA_ID --> (Comma ID COMMA_ID) | ε
VAR_ASSIGN --> MULTI_ID Asignar EXPRESSION
FUNCTION_CALL --> BUILDIN_FUNC InGroup MULTI_EXPRESSION EndGroup
MULTI_EXPRESSION --> (EXPRESSION COMMA_EXPRESSION) | ε
COMMA_EXPRESSION --> (Comma EXPRESSION COMMA_EXPRESSION) | ε
BUILDIN_FUNC --> ID | FileFuncs | MathFunc | StringFunc | GammaFunction | ConsoleOutputFunc | ConsoleInputFunc

EXPRESSION --> Texto | NUMERICAL_EXPRESSION | BoolValue
NUMERICAL_EXPRESSION --> TERMINO NUMERICAL_EXPRESSION_PRIME
NUMERICAL_EXPRESSION_PRIME --> (PLUS TERMINO NUMERICAL_EXPRESSION_PRIME) | (MINUS TERMINO NUMERICAL_EXPRESSION_PRIME) | ε
TERMINO --> FACTOR TERMINO_PRIME
TERMINO_PRIME --> (STAR FACTOR TERMINO_PRIME) | (SLASH FACTOR TERMINO_PRIME) | ε
FACTOR --> ID | FUNCTION_CALL | Entero | Flotante | (InGroup NUMERICAL_EXPRESSION EndGroup)

BOOL_EXPRESSION --> BOOL_TERM BOOL_EXPRESSION_PRIME
BOOL_EXPRESSION_PRIME --> (LogOp BOOL_TERM BOOL_EXPRESSION_PRIME) | ε
BOOL_TERM --> ID | BoolValue | FUNCTION_CALL | REL_EXPRESSION | UNI_EXPRESSION | (InBracket BOOL_EXPRESSION EndBracket)
REL_EXPRESSION --> (NUM